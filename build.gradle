buildscript {
    repositories {
        maven {
            name 'forge'
            url = 'https://maven.minecraftforge.net/'
        }
        maven {
            name 'sponge'
            url 'https://repo.spongepowered.org/repository/maven-public/'
        }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.1.+', changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version = '0.0.1-alpha'
group = 'mods.railcraft'
archivesBaseName = 'railcraft'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: 'official', version: "${minecraft_version}"

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run/client')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            
            args '--mixin', 'railcraft.mixins.json'

            mods {
                railcraft {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run/server')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            args '--mixin', 'railcraft.mixins.json'

            mods {
                railcraft {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run/data')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'railcraft', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                railcraft {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        name 'sponge'
        url 'https://repo.spongepowered.org/repository/maven-public/'
    }
}

dependencies {
    minecraft group: 'net.minecraftforge', name:'forge', version: "${minecraft_version}-${forge_version}"
    implementation group: 'org.spongepowered', name: 'mixin', version: "${mixin_version}"
    annotationProcessor("org.spongepowered:mixin:${mixin_version}:processor")
    testAnnotationProcessor("org.spongepowered:mixin:${mixin_version}:processor")
}

jar {
    manifest {
        attributes([
        	"Implementation-Title": project.name,
        	"Implementation-Version": project.version,
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "MixinConfigs": 'railcraft.mixins.json'
        ])
    }
}

mixin {
	add sourceSets.main, "railcraft.refmap.json"
}

jar.finalizedBy('reobfJar') 